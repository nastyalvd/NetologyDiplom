#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// регистр ОсновныеНачисления
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	движения.ВКМ_Удержания.Записывать = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	ПериодРегистрации = НачалоМесяца(Дата);
	НачалоБазовогоПериода = НачалоМесяца(ДобавитьМесяц(ПериодРегистрации, -12));
	КонецБазовогоПериода = КонецМесяца(ДобавитьМесяц(ПериодРегистрации, -1));
	
	Для Каждого ТекСтрокаОсновныеНачисления Из ОсновныеНачисления Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаОсновныеНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(ТекСтрокаОсновныеНачисления.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Если ТекСтрокаОсновныеНачисления.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
			Движение.БазовыйПериодНачало = НачалоБазовогоПериода;
			Движение.БазовыйПериодКонец = КонецБазовогоПериода;
		КонецЕсли;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;		

	КонецЦикла;	
	
	СторноЗаписи = Движения.ВКМ_ОсновныеНачисления.ПолучитьДополнение();
	Для Каждого Запись из СторноЗаписи Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.ВидРасчета = Запись.ВидРасчета;
		Движение.Сотрудник = Запись.Сотрудник;		
		Движение.ПериодДействияНачало = Запись.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Запись.ПериодДействияКонецСторно;
		Движение.ПериодРегистрации = Запись.ПериодРегистрацииСторно;
		Движение.Сторно = Истина;
	КонецЦикла;
		
	Движения.Записать();


	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|ГДЕ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета = &ВидРасчета
		|	И ВКМ_ОсновныеНачисленияДанныеГрафика.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.НомерСтроки,
		|	ВТ.ЗначениеПериодДействия как ВремяПлан,
		|	ВТ.ЗначениеФактическийПериодДействия как ВремяФакт,
		|	МАКСИМУМ(естьnull(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад,0)) КАК Оклад,
		|	СУММА(естьnull(ВКМ_ВыполненныеСотрудникомРаботы.СуммаКОплате,0)) КАК СуммаКОплате
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&МоментВремени, Сотрудник В
		|			(ВЫБРАТЬ
		|				ВТ.Сотрудник
		|			ИЗ
		|				ВТ КАК ВТ)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник = ВТ.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы КАК ВКМ_ВыполненныеСотрудникомРаботы
		|		ПО ВТ.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботы.Сотрудник
		|		И ВКМ_ВыполненныеСотрудникомРаботы.Период <= ВТ.ПериодДействияКонец
		|		И ВКМ_ВыполненныеСотрудникомРаботы.Период >= ВТ.ПериодДействияНачало
		|СГРУППИРОВАТЬ ПО
		|	ВТ.НомерСтроки,
		|	ВТ.ЗначениеПериодДействия,
		|	ВТ.ЗначениеФактическийПериодДействия";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	
	Выборка = Запрос.Выполнить().Выбрать();
	НаборЗаписей = Движения.ВКМ_ОсновныеНачисления;
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Выборка.НайтиСледующий(Новый Структура("НомерСтроки", Запись.НомерСтроки)) Тогда
			Сторно = ?(Запись.Сторно, -1, 1);
			Результат = сторно * ?(Выборка.ВремяПлан = 0, 0, (Выборка.Оклад * Выборка.ВремяФакт / Выборка.ВремяПлан + ?(Запись.Сторно, 0, Выборка.СуммаКОплате)));
			Запись.Результат = Результат;
			Запись.ОтработаноДней = сторно * Выборка.ВремяФакт;
			
			
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.Налог;
			Движение.ПериодРегистрации = Запись.ПериодДействия;
			Движение.Сторно = Запись.Сторно;
			Движение.Результат = Результат * 0.13;
			Движение.Сотрудник = Запись.Сотрудник;

			Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
			Движение.Сотрудник = Запись.Сотрудник;
			движение.Сумма =  Результат;
			Движение.Период  = Запись.ПериодДействия;
			
			Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьРасход();
			Движение.Сотрудник = Запись.Сотрудник;
			Движение.Сумма = Результат * 0.13;
			Движение.Период = Запись.ПериодДействия;	
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|ГДЕ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.Регистратор = &Регистратор
		|	И ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета = &ВидРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза,
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ОтработаноДнейБаза,
		|	ВТ.НомерСтроки,
		|	ВТ.ЗначениеФактическийПериодДействия
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,,) КАК
		|			ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
		|		ПО ВТ.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
		|ГДЕ
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.Регистратор = &Регистратор
		|	И ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ВидРасчета = &ВидРасчета";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Измерения = Новый Массив();
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Выборка.НайтиСледующий(Новый Структура("НомерСтроки", Запись.НомерСтроки)) Тогда
			результат = ?(Выборка.ОтработаноДнейБаза = 0, 0, (Выборка.РезультатБаза / Выборка.ОтработаноДнейБаза * Выборка.ЗначениеФактическийПериодДействия));
			Запись.Результат = Результат;
			Запись.ОтработаноДней = Выборка.ЗначениеФактическийПериодДействия;
			
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.Налог;
			Движение.ПериодРегистрации = Запись.ПериодДействия;
			Движение.Результат = результат*0.13;
			Движение.Сотрудник = Запись.Сотрудник;
			
			Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
			Движение.Сотрудник = Запись.Сотрудник;
			движение.Сумма =  Результат;
			Движение.Период  = Запись.ПериодДействия;
			
			Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьРасход();
			Движение.Сотрудник = Запись.Сотрудник;
			Движение.Сумма =  Результат * 0.13;
			Движение.Период  = Запись.ПериодДействия;	
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	НаборЗаписей.Записать(,Истина);
	Движения.ВКМ_Удержания.Записать();
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
		

КонецПроцедуры

#КонецОбласти

#КонецЕсли