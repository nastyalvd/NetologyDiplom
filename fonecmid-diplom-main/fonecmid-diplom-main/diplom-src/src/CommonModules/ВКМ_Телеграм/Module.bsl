#Область СлужебныеПроцедурыИФункции

Функция ОтправитьСообщениеТелеграм(ТекстСообщения) Экспорт

	Соединение = Новый HTTPСоединение("api.telegram.org", , , , , ,Новый ЗащищенноеСоединениеOpenSSL());
	
	АдресЗапроса = СтрШаблон("/bot%1/sendMessage", Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить());
	ЗаголовокЗапросаHTTP = Новый Соответствие();
	ЗаголовокЗапросаHTTP.Вставить("Content-Type", "application/json");

	Запрос = Новый HTTPЗапрос(АдресЗапроса, ЗаголовокЗапросаHTTP);
	
	Структура = Новый Структура;
	Структура.Вставить("chat_id", Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить());
	Структура.Вставить("text", ТекстСообщения);
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Структура);
		
	Запрос.УстановитьТелоИзСтроки(ЗаписьJSON.Закрыть());

	Ответ = Соединение.Получить(Запрос);
	
	Если НЕ Ответ.КодСостояния = 200 Тогда
		ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТекстОтвета);
		ОбъектИзJSON = ПрочитатьJSON(ЧтениеJSON, Истина);
		ЧтениеJSON.Закрыть();
	
		ЗаписьЖурнала = Новый Структура;
		ЗаписьЖурнала.Вставить("ИмяСобытия", "Ошибка при отправке сообщения в телеграм");
		ЗаписьЖурнала.Вставить("ПредставлениеУровня", "Ошибка");
		ЗаписьЖурнала.Вставить("Комментарий", ОбъектИзJSON["description"]);
		ЗаписьЖурнала.Вставить("ДатаСобытия");
		
		СобытияДляЖурналаРегистрации = Новый СписокЗначений();
		СобытияДляЖурналаРегистрации.Добавить(ЗаписьЖурнала);
		
		ЖурналРегистрации.ЗаписатьСобытияВЖурналРегистрации(СобытияДляЖурналаРегистрации);					
	КонецЕсли;
	
	Возврат Ответ.КодСостояния;
			
КонецФункции


Процедура СоздатьУведомление(Сообщение) Экспорт
	НовыйЭлемент = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
	НовыйЭлемент.ТекстСообщения = Сообщение;
	НовыйЭлемент.Записать();
КонецПроцедуры


Процедура ВКМ_ОтправкаУведомленийТГ() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграмБоту.Ссылка,
		|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КодОтвета = ОтправитьСообщениеТелеграм(Выборка.ТекстСообщения);	
		Если КодОтвета = 200 Тогда
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.Удалить();	
		КонецЕсли;		
	КонецЦикла;
КонецПроцедуры


#КонецОбласти